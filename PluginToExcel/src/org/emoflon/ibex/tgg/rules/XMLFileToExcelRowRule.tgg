#using PluginsToExcel.*
#using AttrCondDefLibrary.*

#rule XMLFileToExcelRowRule #with PluginsToExcel

#source {
	xmlFolderDrive:Folder{
		-subFolder->xmlFolder_1
	}
	xmlFolder_1:Folder{
		-subFolder->xmlFolder_2
	}
	xmlFolder_2:Folder{
		++-subFolder->pluginFolder
	}
	
	++pluginFolder:Folder
}

#target {
	
	//create cell for new row for every file
	++cell_1:Cell
	++cell_2:Cell{
		text:=""
	}
	++cell_3:Cell{
		text:=""
	}
	++cell_4:Cell{
		text:=""
	}
	
	//create new row for file
	++blockheaderRow:Row{
		++-cell->cell_1
		++-cell->cell_2
		++-cell->cell_3
		++-cell->cell_4
	}
	
	//add connection to last row
	headerRow:Row{
		++-nextRow->blockheaderRow
	}
	
	//add cells to existing column
	componentsColumn:Column{
		++-cell->cell_1
		-nextColumn->dependenciesColumn
	}
	dependenciesColumn:Column{
		++-cell->cell_2
		-nextColumn->documentationColumn
	}
	documentationColumn:Column{
		++-cell->cell_3
		-nextColumn->reviewColumn
	}
	reviewColumn:Column{
		++-cell->cell_4
	}
	
	
	excelFile:File {
		-sheet->sheet
	}
	sheet:Sheet {
		//add new row
		++-rowobject->blockheaderRow
		
		//add new cells
		++-cell->cell_1
		++-cell->cell_2
		++-cell->cell_3
		++-cell->cell_4
		
		//existing elements
		-rowobject->headerRow
		-colobject->componentsColumn
		-colobject->dependenciesColumn
		-colobject->documentationColumn
		-colobject->reviewColumn
	}
}

#correspondence {
	xmlFolderToExcelFileRule:XMLFolderToExcelFileRelation{
		#src->xmlFolder_2
		#trg->excelFile
	}
	
	++ fileToBlockHeaderRowRelation:PluginFolderToExcelBlockHeaderRowRelation{
		#src->pluginFolder
		#trg->blockheaderRow
	}
}

#attributeConditions {
	eq_string(cell_1.text, pluginFolder.name)
}


